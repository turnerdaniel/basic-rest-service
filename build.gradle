plugins {
	id 'org.springframework.boot' version '2.5.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.sonarqube' version '3.3'
}

allprojects {
	version '2.0.0'
	group 'uk.co.danielturner'

	repositories {
		mavenCentral()
	}
}

sonarqube {
	properties {
		property 'sonar.projectName', 'Notes Manager'
		property 'sonar.projectKey', 'notes-manager'
		property 'sonar.organization', 'turnerdaniel'
		property 'sonar.host.url', 'https://sonarcloud.io'
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'

	jar {
		enabled = true
	}

	test {
		useJUnitPlatform()
		finalizedBy jacocoTestReport
	}

	jacocoTestReport {
		dependsOn test
		reports {
			html.required = false
			xml.required = true
			xml.outputLocation = layout.buildDirectory.file("reports/jacoco/coverage.xml")
		}
	}
}
